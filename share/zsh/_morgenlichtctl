#compdef morgenlichtctl

# complete an alarm name
# $1: enabled or disabled
_alarm_name() {
  if [[ "$1" == "enabled" ]]; then
    _values 'enabled alarm name' $(morgenlichtctl alarm list --csv | tail -n +2 | grep Enabled | cut -d, -f 1)
  elif [[ "$1" == "disabled" ]]; then
    _values 'disabled alarm name' $(morgenlichtctl alarm list --csv | tail -n +2 | grep Disabled | cut -d, -f 1)
  else
    _values 'alarm name' $(morgenlichtctl alarm list --csv | tail -n +2 | cut -d, -f 1)
  fi
}

# complete the main command
_main_commands() {
  _arguments -s : \
    {-h,--help}'[show help]' \
    {-v,--version}'[show version]' \
    "1:main command:((server-info\:'query server information' alarm\:'manage alarms' set-leds\:'directly set leds (R1 R2 G1 G2 B1 B2)'))"
}

# complete the alarm command
_alarm_commands() {
  _arguments -s : \
    "2:alarm command:((list\:'list alarms and status' enable\:'enable an alarm' disable\:'disable an alarm' delete\:'delete an alarm' add\:'add or edit an alarm'))"
}

# complete the alarm add command
_alarm_add() {
  _arguments -s : \
    "3:HH\:MM\:SS:()" \
    "-n[alarm name]:alarm name:_alarm_name" \
    "-c[color]:color profile:()" \
    "-d[days]:day:_weekdays" \
    "-s[sound]:sound file:()"
}

# complete week days
_weekdays() {
  _values 'week days' {Mon,}{Tue,}{Wed,}{Thu,}{Fri,}{Sat,}{Sun,}
}

# main dispatcher
_morgenlichtctl() {
  case $words[CURRENT-1] in
    # level 2
    (server-info|set-leds)
      _message 'no more options'
      lstate=""
      ;;
    (alarm)
      _alarm_commands
      lstate=""
      ;;

    # level 3
    (enable)
      _alarm_name "disabled"
      lstate=""
      ;;
    (disable)
      _alarm_name "enabled"
      lstate=""
      ;;
    (delete)
      _alarm_name
      lstate=""
      ;;
    (add)
      _alarm_add
      lstate="alarm_add"
      ;;

    (*) case $lstate in
      # options to alarm add
      (alarm_add) _alarm_add ;;
      # level 1
      (*) _main_commands ;;
    esac
  esac
}

_morgenlichtctl "$@"
lstate=""

# vim: set et sw=2 ts=2 ft=zsh :
